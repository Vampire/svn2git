_realname=subversion
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.14.0
pkgrel=1
pkgdesc="A Modern Concurrent Version Control System (mingw-w64)"
arch=('i686' 'x86_64')
url="https://subversion.apache.org/"
license=('APACHE')
groups=('VCS')
makedepends=("autoconf"
             "${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-libtool"
             "${MINGW_PACKAGE_PREFIX}-apr"
             "${MINGW_PACKAGE_PREFIX}-apr-util"
             "${MINGW_PACKAGE_PREFIX}-sqlite3"
             "${MINGW_PACKAGE_PREFIX}-zlib"
             "${MINGW_PACKAGE_PREFIX}-lz4"
             "${MINGW_PACKAGE_PREFIX}-libutf8proc")
provides=("${MINGW_PACKAGE_PREFIX}-svn")
noextract=("${_realname}-${pkgver}.tar.bz2")
#source=(https://www.apache.org/dist/subversion/${_realname}-${pkgver}.tar.bz2
#        https://www.sqlite.org/2015/sqlite-amalgamation-3081101.zip)
#sha512sums=('af6b706fdc91f7ab292fce9d9de582da306fd11e92767dc852687e71a6a8b65bb867fa70d5afd7f76a46005acb1b3c2d3193e690def48cd26875b3a7851cd13b' 'SKIP')
#sha1sums=('SKIP' '0dbb29c71c4385d1000c091f14475106784daceb')
source=(https://www.apache.org/dist/subversion/${_realname}-${pkgver}.tar.bz2)
sha512sums=('af6b706fdc91f7ab292fce9d9de582da306fd11e92767dc852687e71a6a8b65bb867fa70d5afd7f76a46005acb1b3c2d3193e690def48cd26875b3a7851cd13b')

prepare() {
  [[ -d ${srcdir}/${_realname}-${pkgver} ]] && rm -rf -d ${srcdir}/${_realname}-${pkgver}
  tar -xjvf ${SRCDEST}/${_realname}-${pkgver}.tar.bz2 -C ${srcdir} || true

  cd ${srcdir}/${_realname}-${pkgver}
  #mv ${srcdir}/sqlite-amalgamation-3081101 sqlite-amalgamation
  ./autogen.sh
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  ../${_realname}-${pkgver}/configure \
    --prefix="${MINGW_PREFIX}" \
    --build=${MINGW_CHOST} \
    --with-utf8proc=internal \
    --without-serf \
    --enable-shared --disable-static \
    --disable-mod-activation \
    --enable-local-library-preloading \
    || cat config.log

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
